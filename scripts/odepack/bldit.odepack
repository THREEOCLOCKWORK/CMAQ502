#! /bin/csh -f

# ===================== ODEPACK Build Script ======================== #
# Usage: bldit.odepack >&! bldit.odepack.log                          #
# Requirements: a Fortran compiler                                    #
# Note that this script is configured/tested for Red Hat Linux O/S    #
# The following environment variables must be set for this script to  #
# build an executable.                                                #
#   setenv M3MODEL <source code repository>                           #
#   setenv M3LIB   <code libraries>                                   #
# To report problems or request help with this script/program:        #
#             http://www.cmascenter.org/html/help.html                #
# =================================================================== #

#> Source the config.cmaq file to set the build environment
 source ../config.cmaq

#> Check for M3HOME and M3LIB settings:
 if ( ! -e $M3HOME || ! -e $M3LIB ) then
    echo "   $M3HOME or $M3LIB directory not found"
    exit 1
 endif
 echo "    Model repository base path: $M3HOME"
 echo "                  library path: $M3LIB"

#> If $M3MODEL not set, default to $M3HOME
 if ( $?M3MODEL ) then
    echo "         Model repository path: $M3MODEL"
 else
    setenv M3MODEL $M3HOME
    echo " default Model repository path: $M3MODEL"
 endif

 set BLD_OS = `uname -s`        ## Script set up for Linux only 
 if ($BLD_OS != 'Linux') then
    echo "   $BLD_OS -> wrong bldit script for host!"
    exit 1
 endif

 set echo

#:#:#:#:#:#:#:#:#:#:#:Begin User Input Section #:##:#:#:#:#:#:#:#:#:#:#

#> user choices: Git repository
#git --work-tree=$M3MODEL status
#git status
 setenv REPOROOT ${M3MODEL}/CCTM_APT

 set BASE = $cwd
 set DEST = ${M3LIB}/odepack   # location of output library files
 if ( ! -d "$DEST" ) mkdir -p $DEST
 set RM = "/bin/rm -f"
 set MV = "/bin/mv -f"

#> Set full path of Fortran 90 compiler
 set FC   = ${myFC}
 set FSTD = "${myFFLAGS}"

#set DEBUG     # Uncomment to create debug version of library
 if ( $?DEBUG ) then
    set F_FLAGS = "${FSTD} -c -g"
 else
    set F_FLAGS = "${FSTD} -c"
 endif

#:#:#:#:#:#:#:#:#:#:#:# End of User Input Section :#:#:#:#:#:#:#:#:#:#:#:#:#

 set Bld = $BASE/BLD
 if ( ! -e "$Bld" ) then
    mkdir $Bld
    else
    if ( ! -d "$Bld" ) then
       echo "   *** target exists, but not a directory ***"
       exit 1
    endif
 endif

 cd $Bld

 echo " "
 echo "Host: " `hostname`
 echo "Host operating system:       $BLD_OS"
 echo "Libraries/objects/f90-modules directory: $DEST"
 echo " "

 if ( $?DEBUG ) then
    set Arc = libodepack.debug.a
 else
    set Arc = libodepack.a
 endif
 if ( -e "$Arc" ) $RM $Arc *.mod

#> extract source files from code repository
 cp ${REPOROOT}/SCICHEM/odepack/* $Bld

 set flist = (\
      adjlr.f\
      aigbt.f\
      ainvg.f\
      bnorm.f\
      cdrv.f\
      cfode.f\
      cntnzu.f\
      decbt.f\
      ewset.f\
      fnorm.f\
      intdy.f\
      iprep.f\
      isamax.f\
      jgroup.f\
      lsoda.f\
      lsodar.f\
      lsode.f\
      lsode_bd.f\
      lsodes.f\
      lsodi.f\
      lsoibt.f\
      md.f\
      mdi.f\
      mdm.f\
      mdp.f\
      mdu.f\
      nnfc.f\
      nnsc.f\
      nntc.f\
      nroc.f\
      nsfc.f\
      odrv.f\
      pjibt.f\
      prep.f\
      prepj.f\
      prepji.f\
      prja.f\
      prjs.f\
      r1mach.f\
      rchek.f\
      roots.f\
      saxpy.f\
      scopy.f\
      sdot.f\
      sgbfa.f\
      sgbsl.f\
      sgefa.f\
      sgesl.f\
      slsbt.f\
      slss.f\
      solbt.f\
      solsy.f\
      srcar.f\
      srcma.f\
      srcms.f\
      srcom.f\
      sro.f\
      sscal.f\
      stoda.f\
      stode.f\
      stodi.f\
      vmnorm.f\
      vnorm.f\
      xerrwv.f\
      xsetf.f\
      xsetun.f )

#> compile flist
 foreach file ( $flist )
    $FC $F_FLAGS $file
 end

 ar rv $Arc *.o
 chmod 444 $Arc
 if ( -e "$DEST/$Arc" ) $MV $DEST/$Arc $DEST/$Arc.old
 $MV $Arc $DEST
 $MV *.mod $DEST

 if ( $?DEBUG ) then
    $MV *.o $DEST
 else
    $RM *.o
 endif
 echo " "; echo " Finish building $DEST/$Arc "
 exit()
